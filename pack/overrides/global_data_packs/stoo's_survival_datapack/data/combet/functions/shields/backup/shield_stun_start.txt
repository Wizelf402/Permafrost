# Ran by advancement
# Ran as player
advancement revoke @s only combet:shields/blocked_heavy_damage_adv

### GOAL: Replace vanilla shield-blocking
##	After blocking 8+ damage, the shield reduces attack speed, knockback resistance 
##	Every hit increases shield stun by 2.5s, for up to 5s

### PROCEDURE: Modify the value of shields found in either the player's mainhand or offhand

# SCOREBOARD: This makes sure the cooldown is cumulative, but not too punishing.
execute if score shieldStunning StooSettings matches 1 run scoreboard players add @s StooShields 5
scoreboard players set @s[scores={StooShields=10..}] StooShields 10

# EFFECTS:
execute as @s[scores={StooShields=1..}] positioned ^ ^ ^0.8 run particle crit ~ ~1.1 ~ 0 0 0 0.3 16 force @a
execute as @s[scores={StooShields=1..}] positioned ^ ^ ^0.8 run particle block stripped_spruce_log ~ ~1.1 ~ 0.1 0.1 0.1 0.3 32 force @a
execute at @s[scores={StooShields=1..}] run playsound entity.player.attack.knockback player @a ~ ~ ~ 0.5
execute at @s[scores={StooShields=1..}] run playsound item.axe.strip player @a ~ ~ ~ 1 1.3

# CHECK OFFHAND: (Slot -106)
item entity @s[nbt={Inventory:[{id:"minecraft:shield",Slot:-106b}]}] weapon.offhand modify combet:shields/on_cooldown

# CHECK HOTBAR: (Slots 0-8)
item entity @s[scores={StooShields=1..},nbt={Inventory:[{id:"minecraft:shield",Slot:0b}]}] hotbar.0 modify combet:shields/on_cooldown
item entity @s[scores={StooShields=1..},nbt={Inventory:[{id:"minecraft:shield",Slot:1b}]}] hotbar.1 modify combet:shields/on_cooldown
item entity @s[scores={StooShields=1..},nbt={Inventory:[{id:"minecraft:shield",Slot:2b}]}] hotbar.2 modify combet:shields/on_cooldown
item entity @s[scores={StooShields=1..},nbt={Inventory:[{id:"minecraft:shield",Slot:3b}]}] hotbar.3 modify combet:shields/on_cooldown
item entity @s[scores={StooShields=1..},nbt={Inventory:[{id:"minecraft:shield",Slot:4b}]}] hotbar.4 modify combet:shields/on_cooldown
item entity @s[scores={StooShields=1..},nbt={Inventory:[{id:"minecraft:shield",Slot:5b}]}] hotbar.5 modify combet:shields/on_cooldown
item entity @s[scores={StooShields=1..},nbt={Inventory:[{id:"minecraft:shield",Slot:6b}]}] hotbar.6 modify combet:shields/on_cooldown
item entity @s[scores={StooShields=1..},nbt={Inventory:[{id:"minecraft:shield",Slot:7b}]}] hotbar.7 modify combet:shields/on_cooldown
item entity @s[scores={StooShields=1..},nbt={Inventory:[{id:"minecraft:shield",Slot:8b}]}] hotbar.8 modify combet:shields/on_cooldown

# CROSSHAIR INDICATOR:
title @s reset
title @s[scores={StooShields=..5}] times 3 2 50
title @s[scores={StooShields=6..7}] times 3 2 70
title @s[scores={StooShields=8..}] times 3 30 70
title @s title [{"text":" "}]
title @s subtitle [{"text":"\uABCD"}]

# SCHEDULING:
execute if score shieldStunning StooSettings matches 1 run schedule function combet:shields/shield_cooldown_clock 10t

